buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url("https://plugins.gradle.org/m2/")
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlin_version}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlin_version}")
        classpath("org.jmailen.gradle:kotlinter-gradle:1.26.0")
    }
}

allprojects {
    apply plugin: 'idea'

    group = 'kr.co.lendit'
    version = '0.0.1-SNAPSHOT'

    repositories {
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'kotlin-noarg'

    sourceCompatibility = JavaVersion.VERSION_1_8

    archivesBaseName = "$rootProject.name-$project.name"

    dependencies {
        implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
        implementation("org.jetbrains.kotlin:kotlin-reflect")
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

project(":core") {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        api group: 'io.github.microutils', name: 'kotlin-logging', version: '1.6.25'
    }
}

project(":admin") {
    ext {
        retrofitVersion = '2.8.2'
    }
    dependencies {
        implementation project(":core")

        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-web'

        // retrofit
        implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
        implementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"
        implementation "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
        implementation "com.squareup.retrofit2:converter-jaxb:$retrofitVersion"
        implementation "com.squareup.okhttp3:logging-interceptor:3.8.0"
    }
}

project(":web") {
    ext {
        retrofitVersion = '2.5.0'
    }
    dependencies {
        implementation project(":core")

        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
        implementation "io.sentry:sentry-spring:1.6.4"

        // retrofit
        implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
        implementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"
        implementation "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
        implementation "com.squareup.retrofit2:converter-jaxb:$retrofitVersion"
        implementation "com.squareup.okhttp3:logging-interceptor:3.8.0"
    }
}
